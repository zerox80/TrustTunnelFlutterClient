def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { instr ->
        localProperties.load(instr)
    }
}

def getGprProp = { String name ->
    def v = project.findProperty(name)
    if (v != null) return v.toString()

    v = localProperties.getProperty(name)
    if (v != null) return v.toString()

    // fallback to env
    def envName = name.replace('.', '_').toUpperCase()   // gpr.key -> GPR_KEY
    v = System.getenv(envName)
    if (v != null && !v.toString().trim().isEmpty()) return v.toString()

    return null
}


allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/TrustTunnel/TrustTunnelClient")
            credentials {
                username = ""
                password = getGprProp("gpr.key")
            }
            authentication { basic(BasicAuthentication) }
        }
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
