group = "com.adguard.trusttunnel.vpn_plugin"
version = "1.0-SNAPSHOT"

def getGprProp = { String name ->
    def v = project.findProperty(name)
    if (v != null) return v.toString()

    // fallback to env
    def envName = name.replace('.', '_').toUpperCase()   // gpr.key -> GPR_KEY
    v = System.getenv(envName)
    if (v != null && !v.toString().trim().isEmpty()) return v.toString()

    return null
}

buildscript {
    ext.kotlin_version = "2.2.10"
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.9.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/zerox80/TrustTunnelClient")
            credentials {
                username = ""
                password = getGprProp("gpr.key")
            }
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def flutterRoot = rootProject.ext.has('flutterRoot') ? rootProject.ext.flutterRoot : null
if (flutterRoot != null) {
    apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.adguard.trusttunnel.vpn_plugin"
    }
    compileSdk = 34
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = "1.8" }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }
    defaultConfig { minSdk = 21 }
}

dependencies {
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.mockito:mockito-core:5.0.0")
    implementation("com.adguard.trusttunnel:trusttunnel-client-android:0.99.94")
}
